System h2

Dispatch sending_patient_parameter : sending_patient_parameter(PARAMETER, TIMESTAMP)
Dispatch patient_parameters_request : patient_parameters_request
Dispatch patient_parameters_response : patient_parameters_response(LIST)
Dispatch notification : notification(DESCRIPTION)
Dispatch login_response : login_response(DESCRIPTION)
Event    patient_data_request : patient_data_request
Event    doctor_data_request : doctor_data_request
Event    doctor_notification_sender : doctor_notification_sender(NOTIFICATION)
Event    register_request : register_request(NICKNAME,TYPE)
Event    login_request : login_request(NICKNAME, NAME)
Event    patient_login : patient_login
Event    doctor_login : doctor_login


//requirements: business 1,2,3,4 e functional 1,2,3,4,5
Context ctx_data_centre 	ip[host="localhost" port=8070] //-g cyan
Context ctx_patient 		ip[host="localhost" port=1234] //-g green
Context ctx_doctor  		ip[host="localhost" port=3456] //-g white
Context ctx_create_actors  	ip[host="localhost" port=4678] //-g white

EventHandler evh for patient_data_request, doctor_data_request, doctor_notification_sender, register_request, login_request -print;

QActor qpatient_login context ctx_patient {
	
	Plan init normal [
		println(" --- [PL] initializing --- ");
		actorOp createGUI	
	] switchTo waitResponse
	
	Plan waitResponse [
		println(" --- [PL] waiting response --- ")
	] transition stopAfter 86400000
	  whenMsg login_response -> loginResponse
	  finally repeatPlan
	  
	Plan loginResponse [
		println(" --- [PL] login response --- ");
		removeRule login(_);
		onMsg login_response : login_response(DESCRIPTION) -> addRule login(DESCRIPTION);
		[!? login(DESCRIPTION)] actorOp print(DESCRIPTION);
		[?? login("Login ok")] emit patient_login : patient_login
	] switchTo waitResponse
}

QActor qpatient_sender context ctx_patient {
	
	Plan init normal [
		println(" --- [PS] initializing --- ")	
	] switchTo waitLogin
	
	Plan waitLogin [
		println(" --- [PS] waiting login --- ")
	] transition stopAfter 86400000
	  whenEvent patient_login -> sendData
	  finally repeatPlan
	
	Plan sendData [
		println(" --- [PS] sending data --- ");
		forward qdc_data_receiver -m sending_patient_parameter : sending_patient_parameter(37, "12/12/12");
		delay 2500;
		forward qdc_data_receiver -m sending_patient_parameter : sending_patient_parameter(38, "12/12/12");
		delay 2500;
		forward qdc_data_receiver -m sending_patient_parameter : sending_patient_parameter(36, "12/12/12");
		delay 2500
	] finally repeatPlan
	
}

QActor qpatient_data_retriever context ctx_patient {
	
	Plan waitLogin normal[
		println(" --- [PR] waiting login --- ")
	] transition stopAfter 86400000
	  whenEvent patient_login -> init
	  finally repeatPlan
	  
	Plan init [
		println(" --- [PR] successful login --- ");
		actorOp createGUI
	] switchTo waitUserRequest
	  
	Plan waitUserRequest [
		println(" --- [PR] waiting for user request --- ")
	] transition stopAfter 86400000
	  whenEvent patient_data_request -> requestData
	  finally repeatPlan
	
	Plan requestData [
		println(" --- [PR] requesting data --- ");
		//delay 3000;
		forward qdc_data_receiver -m patient_parameters_request : patient_parameters_request
	] switchTo waitResponse
	
	Plan waitResponse resumeLastPlan [
		println(" --- [PR] waiting for a response --- ")
	] transition stopAfter 600000
	  whenMsg patient_parameters_response -> visualiseData
	  
	Plan visualiseData [
		println(" --- [PR] visualizing data --- ");
		onMsg patient_parameters_response : patient_parameters_response(LIST) -> actorOp print(LIST)
	] switchTo waitUserRequest
}

QActor qpatient_notification_receiver context ctx_patient {
	
	Plan waitLogin normal[
		println(" --- [PNR] waiting login --- ")
	] transition stopAfter 86400000
	  whenEvent patient_login -> init
	  finally repeatPlan
	  
	Plan init [
		println(" --- [PNR] successful login --- ");
		actorOp createGUI
	] switchTo waitNotification
	
	Plan waitNotification [
		println(" --- [PNR] waiting notification --- ")
	] transition stopAfter 86400000
	  whenMsg notification -> visualizeNotification
	  finally repeatPlan
	  
	Plan visualizeNotification resumeLastPlan [
		println(" --- [PNR] receive notification --- ");
		onMsg notification : notification(DESCRIPTION) -> actorOp print(DESCRIPTION)
	]
	
}

QActor qdc_data_receiver context ctx_data_centre  {
	
	Rules {
		request_parameters(LIST) :- findall(patient_parameter(X,Y), patient_parameter(X, Y),LIST).
	}
	
	Plan init normal [
		println(" --- [DC] initializing --- ")	
	] switchTo waitMessages
	
	Plan waitMessages [
		println(" --- [DC] waiting messages --- ")
	] transition stopAfter 86400000
	  whenMsg sending_patient_parameter -> saveParameter,
	  whenMsg patient_parameters_request -> handleRequest
	  finally repeatPlan
	  
	Plan saveParameter [
		println(" --- [DC] saving parameters --- ");
		onMsg sending_patient_parameter : sending_patient_parameter(PARAMETER, TIMESTAMP) -> demo asserta(patient_parameter(PARAMETER, TIMESTAMP))
	] switchTo sendToAnalyser
	
	Plan sendToAnalyser [
		println(" --- [DC] sending to analyzer --- ");
		[!? patient_parameter(PARAMETER, TIMESTAMP)] forward qdc_analyser -m sending_patient_parameter : sending_patient_parameter(PARAMETER, TIMESTAMP) 
	] switchTo waitMessages
	
	Plan handleRequest resumeLastPlan [
		println(" --- [DC] handling parameters request --- ");
		onMsg patient_parameters_request : patient_parameters_request -> actorOp memoCurrentCaller;
		[!? request_parameters(LIST)] replyToCaller -m patient_parameters_response : patient_parameters_response(LIST)		
	]
	
}

QActor qdc_analyser context ctx_data_centre {
	
	Rules {
		analyser(VAL, febbre) :- eval(gt, VAL, 37),!.
		analyser(VAL, non_febbre) :- eval(gt, 37, VAL), !.
		analyser(37, non_febbre).
		
		analyse(RESP) :- 
			parameter(PARAMETER),
			analyser(PARAMETER, RESP).
	}
	
	Plan init normal [
		println(" --- [A] initializing --- ")	
	] switchTo waitParameter
	
	Plan waitParameter [
		println(" --- [A] waiting parameter--- ")
	] transition stopAfter 86400000
	  whenMsg sending_patient_parameter -> analyse
	  finally repeatPlan
	  
	Plan analyse resumeLastPlan [
		println(" --- [A] analyzing parameter--- ");
		onMsg sending_patient_parameter : sending_patient_parameter(PARAMETER, TIMESTAMP) -> addRule parameter(PARAMETER);
		[!? analyse(RESP)] println(RESP);
		[!? analyse(RESP)] forward qdc_notification_manager -m notification : notification(RESP);	
		[!? analyse(RESP)] removeRule parameter(PARAMETER)
	]
	
}

QActor qdc_notification_manager context ctx_data_centre {
	
	Rules {
		send_to_doctor(febbre) :- notification(febbre).
	}
	
	Plan init normal [
		println(" --- [NM] initializing --- ")	
	] switchTo waitNotification
	
	Plan waitNotification [
		println(" --- [NM] waiting notification --- ")
	] transition stopAfter 86400000
	  whenMsg notification -> sendNotification
	  finally repeatPlan
	  
	Plan sendNotification resumeLastPlan [
		println(" --- [NM] sending notification --- ");
		onMsg notification : notification(DESCRIPTION) -> addRule notification(DESCRIPTION); 
		[!? send_to_doctor(DESCRIPTION)] forward qdoctor_notification_receiver -m notification : notification(DESCRIPTION);
		[?? notification(DESCRIPTION)] forward qpatient_notification_receiver -m notification : notification(DESCRIPTION)			
	]
	
}

QActor qdc_user_manager context ctx_data_centre {
	
	Rules {
		alreadyRegistered :- registration(NICKNAME, _), registered(NICKNAME,_).
		
		correct_registration :- registration(NICKNAME,TYPE), assert(registered(NICKNAME,TYPE)), retract(registration(_,_)).
		
		can_login(NAME) :- login(NICKNAME,NAME), registered(NICKNAME,_), retract(login(_,_)).
	}
	
	Plan init normal [
		println(" --- [UM] initializing --- ");
		actorOp createGUI
	] switchTo waitNotification
	
	Plan waitNotification [
		println(" --- [UM] waiting notification --- ")
	] transition stopAfter 86400000
	  whenEvent register_request -> registration,
	  whenEvent login_request -> login
	  finally repeatPlan
	  
	Plan registration resumeLastPlan [
		println(" --- [UM] registration --- ");
		onEvent register_request : register_request(NICKNAME, TYPE) -> addRule registration(NICKNAME, TYPE);
		[!? alreadyRegistered] actorOp print("This user name is already used. Choose another one!");
		[!? alreadyRegistered] removeRule registration(_,_);
		[!? correct_registration] actorOp print("Registration complete!")		
	]
	
	Plan login resumeLastPlan [
		println(" --- [UM] login --- ");
		onEvent login_request : login_request(NICKNAME, NAME) -> addRule login(NICKNAME, NAME);
		[!? can_login(NAME)] forward NAME -m login_response : login_response("Login ok");
		[?? login(_, NAME)] forward NAME -m login_response : login_response("Login failed")
		
	]
	
}

QActor qdoctor_login context ctx_doctor {
	
	Plan init normal [
		println(" --- [DL] initializing --- ");
		actorOp createGUI	
	] switchTo waitResponse
	
	Plan waitResponse [
		println(" --- [DL] waiting response --- ")
	] transition stopAfter 120000
	  whenMsg login_response -> loginResponse
	  finally repeatPlan
	  
	Plan loginResponse [
		println(" --- [DL] login response --- ");
		removeRule login(_);
		onMsg login_response : login_response(DESCRIPTION) -> addRule login(DESCRIPTION);
		[!? login(DESCRIPTION)] actorOp print(DESCRIPTION);
		[?? login("Login ok")] emit doctor_login : doctor_login
	] switchTo waitResponse
}

QActor qdoctor_data_retriever context ctx_doctor {
	
	Plan waitLogin normal[
		println(" --- [D] waiting login --- ")
	] transition stopAfter 86400000
	  whenEvent doctor_login -> init
	  finally repeatPlan
	  
	Plan init [
		println(" --- [D] successful login --- ");
		actorOp createGUI
	] switchTo waitUserRequest
	  
	Plan waitUserRequest [
		println(" --- [D] waiting for user request --- ")
	] transition stopAfter 86400000
	  whenEvent doctor_data_request -> requestData
	  finally repeatPlan
	
	Plan requestData [
		//delay 3000;
		println(" --- [D] requesting data --- ");
		forward qdc_data_receiver -m patient_parameters_request : patient_parameters_request
	] switchTo waitResponse
	
	Plan waitResponse resumeLastPlan [
		println(" --- [D] waiting for a response --- ")
	] transition stopAfter 60000
	  whenMsg patient_parameters_response -> visualiseData
	  
	Plan visualiseData [
		println(" --- [D] visualizing data --- ");
		onMsg patient_parameters_response : patient_parameters_response(LIST) -> actorOp print(LIST)
	] switchTo waitUserRequest
	
}

QActor qdoctor_notification_receiver context ctx_doctor {
	
	Plan waitLogin normal[
		println(" --- [DNR] waiting login --- ")
	] transition stopAfter 86400000
	  whenEvent doctor_login -> init
	  finally repeatPlan
	  
	Plan init [
		println(" --- [DNR] successful login --- ");
		actorOp createGUI
	] switchTo waitNotification
	
	Plan waitNotification [
		println(" --- [DNR] waiting notification --- ")
	] transition stopAfter 86400000
	  whenMsg notification -> receiveNotification
	  finally repeatPlan
	  
	Plan receiveNotification resumeLastPlan [
		println(" --- [DNR] receive notification --- ");
		onMsg notification : notification(DESCRIPTION) -> actorOp print(DESCRIPTION)
	]
	
}

QActor qdoctor_notification_sender context ctx_doctor {
	
	Plan waitLogin normal[
		println(" --- [DNS] waiting login --- ")
	] transition stopAfter 86400000
	  whenEvent doctor_login -> init
	  finally repeatPlan
	
	Plan init  [
		println(" --- [DNS] initializing --- ");
		actorOp createGUI	
	] switchTo waitInput
	
	Plan waitInput [
		println(" --- [DNS] waiting input --- ")
	] transition stopAfter 86400000
	  whenEvent doctor_notification_sender -> sendNotification
	  finally repeatPlan
	
	Plan sendNotification [
		println(" --- [DNS] sending notification --- ");
		onEvent doctor_notification_sender : doctor_notification_sender(NOTIFICATION) -> forward qpatient_notification_receiver -m notification : notification(NOTIFICATION);
		actorOp print("advice sent to the patient")
		//delay 60000
	] switchTo waitInput
	
}

QActor qcreate_actors context ctx_create_actors {
	
	Plan init normal [
		println(" --- [CA] initializing --- ")
	] switchTo createActors
	
	Plan createActors [
		println(" --- [CA] creating actors --- ");
		[!? newName(qpatient_data_retriver, Name, N)] demo createActor(Name, 'it.unibo.qpatient_data_retriever.Qpatient_data_retriever')
	] finally repeatPlan 10
	
}

